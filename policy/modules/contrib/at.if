## <summary>At daemon for running a task a single time</summary>

########################################
## <summary>
##	Role access for at
## </summary>
## <param name="role">
##	<summary>
##	Role allowed access
##	</summary>
## </param>
## <param name="domain">
##	<summary>
##	User domain for the role
##	</summary>
## </param>
#
interface(`at_role',`
	gen_require(`
		type at_exec_t;
		type at_t;
		type atd_t;
		type at_job_log_t;
		type at_job_t;
	')

	##############################
	#
	# Declarations
	#

	role $1 types at_t;

	##############################
	#
	# Local policy
	#

	domtrans_pattern($2, at_exec_t, at_t)

	allow $2 at_t:process signal_perms;

	ps_process_pattern($2, at_t)

	allow atd_t $2:process transition;
	allow atd_t $2:fd use;
	allow atd_t $2:key manage_key_perms;
	dontaudit atd_t $2:process { noatsecure siginh rlimitinh };

	allow $2 atd_t:process sigchld;
	allow $2 atd_t:fd use;

	allow $2 at_job_t:file read_inherited_file_perms;
	allow $2 at_job_log_t:file rw_inherited_file_perms;

	corecmd_shell_entry_type($2)
')

########################################
## <summary>
##	Read from and write to the the inherited atd
##	joblog file
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access
##	</summary>
## </param>
#
interface(`at_rw_inherited_job_log_files',`
	gen_require(`
		type at_job_log_t;
	')

	allow $1 at_job_log_t:file rw_inherited_file_perms;
')
